//leer 2 arreglos de 5 numeros enteros cada uno,
//Que estaran ordenados crecientemente. Copiar (fusionar) los dos arreglos 
//en un tercero, de forma que los numeros sigan ordenados.

Proceso ejercicio10
	definir  i, j, k,  arreglo1, arreglo2, arreglo3 como entero;
	definir creciente como logico;
	dimension arreglo1[5];
	dimension arreglo2[5];
	dimension arreglo3[10];
	
	
	repetir 
		creciente <- verdadero;
		escribir "Arreglo 1";
		para i <- 0 hasta 4 con paso 1 Hacer
			escribir (i+1), ". Digite los numeros";
			leer arreglo1[i];
		FinPara
		Para i<-0 Hasta 3 con paso 1 Hacer
			Si arreglo1[i]>arreglo1[i+1] Entonces
				creciente <- falso;
			FinSi
		FinPara
		
		Si creciente=falso Entonces
			Escribir ' El arreglo no esta ordenado, digite nuevamente';
		FinSi
	Hasta Que creciente = Verdadero
	
	escribir "";
	repetir 
		creciente <- verdadero;
		escribir "Arreglo 2";
		para i <- 0 hasta 4 con paso 1 Hacer
			escribir (i+1), ". Digite los numeros";
			leer arreglo2[i];
		FinPara
		Para i<-0 Hasta 3 con paso 1 Hacer
			Si arreglo2[i]>arreglo2[i+1] Entonces
				creciente <- falso;
			FinSi
		FinPara
		
		Si creciente=falso Entonces
			Escribir ' El arreglo no esta ordenado, digite nuevamente';
		FinSi
	Hasta Que creciente = Verdadero
	
	
	i <-0; //para arreglo1
	j <- 0; //arreglo2
	k <- 0; //arreglo3
	Mientras (i < 5 y j < 5) hacer
		si arreglo1[i] < arreglo2[j] entonces
			arreglo3[k] <- arreglo1[i];
			i <- i+1;
		SiNo//elemento arreglo2 es el menor
		 arreglo3[k] <- arreglo2[j];
		 j <- j+1;
		 
			
			
		FinSi
		k <- k+1; //avanza 1 el iterador de arreglo3
		
		
		
	finmientras
	
	si (i = 5) entonces //terminamos de copiar arreglo1
		mientras (j<5) Hacer //se copian los elementos que faltan de arreglo2
			arreglo3[k] <- arreglo2[j];
			j <- j +1;
			k <- k+1;
		FinMientras
	SiNo
		si (j=5) entonces //se termino de copiar el arreglo2 faltan de arr1
			mientras (i < 5 ) hacer
				arreglo3[k] <- arreglo1[i];
				i <- i+1;
				k <- k+1;
			FinMientras
		FinSi
	FinSi
	
	escribir "";
	escribir "El arreglo 3 es el siguiente";
	
	para i<- 0 hasta 9 con paso 1 Hacer
		escribir "Elemento: ", arreglo3[i];
	FinPara
	
	
FinProceso
